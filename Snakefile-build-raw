"""
Snakefile for doing the first stages of data processing from the daq sandbox files
to the blinded raw data. It handles:
- moving the daq files from the sandbox to the sorted file system
- running build raw on this data (with trimming)
- blinding the physics data
"""

import pathlib, os, json, sys
from scripts.util.patterns import (
    get_pattern_unsorted_data,
    get_pattern_tier_daq,
    )
from scripts.util.utils import  (
    subst_vars_in_snakemake_config,
    runcmd,
    config_path,
    chan_map_path,
    filelist_path,
    )
from scripts.util.pars_loading import pars_catalog



# Set with `snakemake --configfile=/path/to/your/config.json`
# configfile: "have/to/specify/path/to/your/config.json"

subst_vars_in_snakemake_config(workflow, config)

setup = config["setups"]["l200"]
configs = config_path(setup)
chan_maps = chan_map_path(setup)
swenv = runcmd(setup)

basedir = workflow.basedir

include: "rules/common.smk"
include: "rules/main.smk"
include: "rules/raw.smk"

localrules:
    gen_filelist,
    autogen_output,

onstart:
    print("Starting workflow")


onsuccess:
    print("Workflow finished, no error")
    shell("rm *.gen || true")
    shell(f"rm {filelist_path(setup)}/* || true")

rule sort_data:
    """
    This rules moves the daq data from the unsorted sandbox dir
    to the sorted dirs under generated
    """
    input:
        get_pattern_unsorted_data(setup),
    output:
        get_pattern_tier_daq(setup),
    shell:
        "mv {input} {output}"